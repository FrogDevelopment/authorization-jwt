plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '3.2.0'
    id 'jacoco'
}

group = 'fr.frogdevelopment.module'
version = '0.0.1-SNAPSHOT'
description = 'authorization-jwt-module'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    maven {
        url = 'http://nexus.frog-development.com/repository/maven-public/'
    }
}

dependencies {
    api 'io.jsonwebtoken:jjwt:0.9.1'

    implementation 'org.springframework.boot:spring-boot:2.1.3.RELEASE'
    implementation 'org.springframework.security:spring-security-config:5.1.4.RELEASE'
    implementation 'org.springframework.security:spring-security-web:5.1.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure:2.1.3.RELEASE'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'javax.servlet:javax.servlet-api:4.0.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    compileOnly 'org.jetbrains:annotations:17.0.0'

    testImplementation 'org.assertj:assertj-core:3.12.1'
    testImplementation 'org.springframework.security:spring-security-test:5.1.4.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.25.0'
    
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

test {
    reports.html.enabled = false

    useJUnitPlatform {
        includeTags 'unitTest'
        excludeTags 'integrationTest'
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "http://nexus.frog-development.com/repository/maven-releases"
            def snapshotsRepoUrl = "http://nexus.frog-development.com/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
