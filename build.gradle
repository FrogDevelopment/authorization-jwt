plugins {
    id 'jacoco'
    id 'java-library'
    id 'maven'
    id 'org.sonarqube' version '2.8'
    id 'io.freefair.lombok' version '5.0.0'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version "1.0.9.RELEASE"
}

group = 'com.frog-development'
description = 'authorization-jwt-module'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR3"
    }
}

dependencies {
    def jwtVersion = '0.9.1'

    api "io.jsonwebtoken:jjwt:$jwtVersion"

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    compileOnly 'org.jetbrains:annotations:19.0.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.platform:junit-platform-runner'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jgitver {
    strategy = 'PATTERN'
    versionPattern = '${v}-SNAPSHOT'
    tagVersionPattern = '${v}'
}

test {
    reports.html.enabled = false

    useJUnitPlatform {
        includeTags 'unitTest'
        excludeTags 'integrationTest'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/test.xml")
        csv.enabled false
        html.enabled false
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test.xml"
        property "sonar.projectKey", "FrogDevelopment_authorization-jwt-module"
        property "sonar.organization", "frogdevelopment"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

jar {
    enabled true
}

wrapper {
    gradleVersion = "6.2.1"
    distributionType = Wrapper.DistributionType.ALL
}
