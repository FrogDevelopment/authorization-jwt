plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '3.2.0'
    id 'jacoco'
    id 'org.sonarqube' version '2.7'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.7.0'
}

group = 'fr.frogdevelopment.module'
description = 'authorization-jwt-module'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    maven {
        url = "$repoUrl/maven-public/"
    }
}

dependencies {
    api 'io.jsonwebtoken:jjwt:0.9.1'

    implementation 'org.springframework.boot:spring-boot:2.1.3.RELEASE'
    implementation 'org.springframework.security:spring-security-config:5.1.4.RELEASE'
    implementation 'org.springframework.security:spring-security-web:5.1.4.RELEASE'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure:2.1.3.RELEASE'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'javax.servlet:javax.servlet-api:4.0.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    compileOnly 'org.jetbrains:annotations:17.0.0'

    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'org.springframework.security:spring-security-test:5.1.4.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.4.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.25.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jgitver {
    mavenLike true
}

test {
    reports.html.enabled = false

    useJUnitPlatform {
        includeTags 'unitTest'
        excludeTags 'integrationTest'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/test.xml")
        csv.enabled false
        html.enabled false
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test.xml"
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }

    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }
            afterEvaluate {
                def releasesRepoUrl = "$repoUrl/maven-releases/"
                def snapshotsRepoUrl = "$repoUrl/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
